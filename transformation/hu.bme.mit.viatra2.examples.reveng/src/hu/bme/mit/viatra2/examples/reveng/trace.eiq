package hu.bme.mit.viatra2.examples.reveng

import "http://www.emftext.org/java/references"
import "http://www.emftext.org/java/classifiers"
import "http://www.emftext.org/java/commons"

import "http://planet-mde.org/ttc2011/statemachine"

@QueryBasedFeature(source = st,
	feature = "source", target = cl
)
pattern class2State(cl : Class, st : State) {
	Class.name(cl, name);
	State.name(st, name);
}

@QueryBasedFeature(source = t, feature = "source", target = ref)
pattern ref2Transition(srcCl : Class, dstCl : Class, t : Transition, ref : IdentifierReference) {
	Transition.src(t, srcState);
	find class2State(srcCl, srcState);
	
	Transition.dst(t, dstState);
	find class2State(dstCl, dstState);
	
	find classCalledWithActivate(dstCl, ref, srcCl);
}

pattern stateWithoutClass(st : State) {
	neg find class2State(_, st);
}

pattern transitionWithoutReference(t : Transition) {
	neg find ref2Transition(_, _, t, _);
}